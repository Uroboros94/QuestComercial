/*****************************************************************************************************
	Fecha : 12/12/2016
	----------------------------------------------------------------------------------------------------
	Detalle :
	Creaci贸n del Modelo: Recaudo de Facturaci贸n
	-------------------------------------------------------------------------------------------------
	Fecha       |   Desarrollador   |   Modificaci贸n
	-------------------------------------------------------------------------------------------------
	12/12/2016  |   malemos         |   Inicio de Configuraci贸n 
*****************************************************************************************************/
QST_Modelledgertrans:	
	LOAD	
		CUENTA_N3,		
		CUENTA_N4, 
		CUENTA_N5,
		ACCOUNTINGDATE, 	
		VOUCHER, 		
		CREDITO, 		
		DEBITO
	FROM
	[..\QVD\QST_Modelledgertrans.QVD]
	(qvd)
	where CUENTA_N2 = '13';		

Mapping
ReteFuente:
	LOAD
		VOUCHER				as Documento,
		1.135				as %Base
	Resident QST_Modelledgertrans
	Where
		CUENTA_N4 = '135515';


Mapping
Factura_Asiento:
	LOAD
		Pago,
		Asiento
	FROM
	[..\QVD\HomologacionPagos.QVD]
	(qvd);


Recaudo_Temp:
	LOAD  
		CUENTA_N5 			as Auxiliar,
		ACCOUNTINGDATE 		as Fecha,
		VOUCHER 			as Documento,
		CREDITO 			as Credito,
		DEBITO				as Debito
	Resident QST_Modelledgertrans
	Where 
		VOUCHER like 'RC*'
		AND CUENTA_N3 = '1305';
	DROP Table QST_Modelledgertrans;

Recaudo_Temp2:
	LOAD
		Auxiliar,
		Fecha,
		Documento,
		ApplyMap('ReteFuente', Documento, 1.16)		as %Base,
		ApplyMap('Factura_Asiento', Documento)		as Asiento,
		Num(Debito+Credito)							as Movimiento
	resident Recaudo_Temp;
	DROP Table Recaudo_Temp;

Recaudo_Temp3:
	LOAD
		Auxiliar,
		Date(Fecha,'DD/MM/YYYY')					as FechaRecaudo,
		Documento									as IdRecaudo,
		Asiento 									as IdAsiento,
		%Base,
		Movimiento 									as Movimiento,
		(Movimiento/%Base)*0.16						as ValorIva,
		If(%Base<>1.135,0,(Movimiento/%Base)*0.025)	as ValorReteFuente
	Resident Recaudo_Temp2;
	DROP Table Recaudo_Temp2;

Recaudo:
	LOAD
		IdAsiento									as SK.Asiento,
		IdRecaudo									as Recaudo.IdRecaudo,
		IdAsiento									as Recaudo.IdAsiento,
		Auxiliar									as Recaudo.Cuenta,
		FechaRecaudo								as Recaudo.FechaRecaudo,
		Movimiento									as Recaudo.Movimiento,
		ValorIva									as Recaudo.ValorIva,
		ValorReteFuente								as Recaudo.ReteFuente
	Resident Recaudo_Temp3;
	DROP Table Recaudo_Temp3;

