/*****************************************************************************************************
Fecha : 05/10/2016
----------------------------------------------------------------------------------------------------
 Detalle :
     Modelo de LinkTable
-------------------------------------------------------------------------------------------------
Fecha       |   Desarrollador   	|   Modificación
-------------------------------------------------------------------------------------------------
05/10/2016  |   Malemos			|   Inicio de Configuración
*****************************************************************************************************/
LinkTable:
	LOAD
		%OrdenesProduccionKey,
		%DEFAULTDIMENSION 					as SK.DEFAULTDIMENSION,
		%KEYPRODUCT							as SK.KEYPRODUCT,
		%FECHAKEY							as SK.FECHA
	Resident OrdenProd;
  Concatenate(LinkTable)
  LOAD
    %OrdenesPedidoKey,
    %DEFAULTDIMENSION 					as SK.DEFAULTDIMENSION,
    %KEYPRODUCT							as SK.KEYPRODUCT,
    %FECHAKEY							as SK.FECHA
  Resident OrdenesPedidosVenta;

  OrdenesCompra_tmp:
  	LOAD
  		%OrdenesCompraKey,
  		%DEFAULTDIMENSION 					as SK.DEFAULTDIMENSION,
  		%KEYPRODUCT							as SK.KEYPRODUCT,
  		%FECHAKEY							as SK.FECHA,
      OrdenesCompra.LineasOC.INVENTREFID
  	Resident OrdenesCompra;
    Left Join(OrdenesCompra_tmp)
    LOAD
      %OrdenesProduccionKey,
      PRODID as OrdenesCompra.LineasOC.INVENTREFID
    Resident OrdenProd;

  Concatenate(LinkTable)
  LOAD
    %OrdenesCompraKey,
    SK.DEFAULTDIMENSION,
    SK.KEYPRODUCT,
    SK.FECHA,
    %OrdenesProduccionKey
  Resident OrdenesCompra_tmp;

  PedidosTransfer_tmp:
  LOAD
    %PedidosTransferKey,
    %KEYPRODUCT							as SK.KEYPRODUCT,
    %FECHAKEY							as SK.FECHA,
    PedidosTransfer.ORDENPRODUCMAESTRA
  Resident PedidosTransfer;
  Left Join(PedidosTransfer_tmp)
  LOAD
    %OrdenesProduccionKey,
    OrdenProd.AP_QST_MASTERPRODORDID as PedidosTransfer.ORDENPRODUCMAESTRA
  Resident OrdenProd;

  Concatenate(LinkTable)
  LOAD
    %PedidosTransferKey,
    %OrdenesProduccionKey,
    SK.FECHA,
    SK.KEYPRODUCT
  Resident PedidosTransfer_tmp;

  RutasPicking_tmp:
  LOAD
    %RutasPickingKey,
    RutasPicking.NUMEROPEDIDO,
    %KEYPRODUCT							as SK.KEYPRODUCT,
    %FECHAKEY							as SK.FECHA
  Resident RutasPicking;
  Left Join (RutasPicking_tmp)
  LOAD
    %OrdenesPedidoKey,
    IDPEDIDO as RutasPicking.NUMEROPEDIDO,
    %KEYPRODUCT as SK.KEYPRODUCT
  Resident OrdenesPedidosVenta;

  Concatenate(LinkTable)
  LOAD
    %RutasPickingKey,
    %OrdenesPedidoKey,
    SK.KEYPRODUCT,
    SK.FECHA
  Resident RutasPicking_tmp;
  Concatenate(LinkTable)
  LOAD
    SK.KEYPRODUCT
  Resident InventarioDisponible;

  MovimientosArticulos_tmp:
  LOAD
    %MovimientoProductoKey,
    %KEYPRODUCT as SK.KEYPRODUCT,
    %FECHAKEY as SK.FECHA,
    MovimientosArticulos.PEDIDOREFERENCIA,
    MovimientosArticulos.ORDENPRODUC,
    %IDLOCALIDAD    as SK.IDLOCALIDAD
  Resident MovimientosArticulos;
  Left Join (MovimientosArticulos_tmp) //Movimientos (entrada, transferencia, salida) de articulos.
  LOAD
    %OrdenesProduccionKey,
    COLLECTREFPRODID as MovimientosArticulos.ORDENPRODUC
  Resident OrdenProd;
  Left Join (MovimientosArticulos_tmp) //Recepcion de articulos.
  LOAD
    %OrdenesCompraKey,
    OrdenesCompra.PURCHID as MovimientosArticulos.PEDIDOREFERENCIA
  Resident OrdenesCompra;

  Concatenate(LinkTable)
  LOAD
    %MovimientoProductoKey,
    %OrdenesProduccionKey,
    %OrdenesCompraKey,
    SK.KEYPRODUCT,
    SK.FECHA,
    SK.IDLOCALIDAD
  Resident MovimientosArticulos_tmp;

	DROP Fields %DEFAULTDIMENSION,%FECHAKEY,%KEYPRODUCT, %IDLOCALIDAD;
  DROP Tables OrdenesCompra_tmp, RutasPicking_tmp, PedidosTransfer_tmp, MovimientosArticulos_tmp;
